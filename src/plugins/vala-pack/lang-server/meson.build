gnome_builder_vala_sources = files(
  '../valaconfig.vapi',
  'gnome-builder-vala.vala',
  'ide-utils.vala',
  'ide-vala-index.vala',
  'ide-vala-completion.vala',
  'ide-vala-locator.vala',
  'ide-vala-diagnostics.vala',
  'ide-vala-symbol-tree.vala',
  'ide-vala-source-file.vala'
)

gnome_builder_vala_deps = [
  libpeas_dep,
  libvala,
  libgiounix_dep,
  libide_vapi,
  libgtksource_dep,
  libvte_dep,
  libdazzle_dep,
  libtemplate_glib_dep,
  libjsonrpc_glib_dep,

  libide_code_dep,
  libide_core_dep,
]

executable('gnome-builder-vala', gnome_builder_vala_sources,
         dependencies: gnome_builder_vala_deps,
              gui_app: false,
              install: true,
          install_dir: get_option('libexecdir'),
            link_args: exe_link_args,
        install_rpath: pkglibdir_abs,
  include_directories: [include_directories('.')] + libide_include_directories,

            vala_args: [ '--target-glib=2.52',
                         '--pkg=posix',
                         '--pkg=libpeas-1.0',
                         '--pkg=gtksourceview-4',
                         '--pkg=gio-2.0',
                         '--pkg=libdazzle-1.0',
                         '--pkg=template-glib-1.0',
                         '--pkg=vte-2.91',
                         '--pkg=libvala-' + libvala_version,
                         '--pkg=jsonrpc-glib-1.0',
                       ],
               c_args: exe_c_args +
                       [ '-DVALA_VERSION="@0@"'.format(libvala_version),
                         '-DLOG_DOMAIN="vala-pack-plugin"',
                         '-DGETTEXT_PACKAGE="gnome-builder"',
                         '-DPACKAGE_DATADIR="@0@"'.format(join_paths(get_option('prefix'), get_option('datadir'), 'gnome-builder')),
                         '-Wno-incompatible-pointer-types',
                       ],
)
